*** Settings ***
Documentation   Test suite which deals with settings related to session scope, hence modifications at some test case can have side effects at another one. Be careful.
Library			JMSLibrary
Suite Setup		Connect And Start    org.apache.activemq.jndi.ActiveMQInitialContextFactory  tcp://localhost:61616?jms.useAsyncSend=false
Suite Teardown	Close
Test Setup              Clear Queue  ${QUEUE}
Test Tear Down          Clear Queue  ${QUEUE}

*** Variables ***
${QUEUE}    	QUEUE.JMSLIBRARY.TEST
${BODY_TEXT}    Hello world!

*** Test Cases ***

Send Client Acknowledged
    Initialize Session  false  2
    Create Text Message  ${BODY_TEXT}
    Send To Queue  ${QUEUE}
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Queue  ${QUEUE}
    Acknowledge
    ${r}=  Get Redelivered

Transaction
    Initialize Session  true  0
    Create Text Message  ${BODY_TEXT}
    Send To Queue  ${QUEUE}
    Commit
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Queue  ${QUEUE}
    Rollback
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Queue  ${QUEUE}
    Commit
    ${c}=  Clear Queue  ${QUEUE}
    Should Be Equal As Integers  0  ${c}

Not Redelivered
    Initialize Session  false  CLIENT_ACKNOWLEDGE
    Create Text Message  ${BODY_TEXT}
    Send To Queue  ${QUEUE}
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Queue  ${QUEUE}
    ${t}=  Get Redelivered
    Should Not Be True  ${t}
    Acknowledge

Redelivered              
    Initialize Session  false  CLIENT_ACKNOWLEDGE
    Create Text Message  ${BODY_TEXT}
    Send To Queue  ${QUEUE}
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Queue  ${QUEUE}
    Initialize Session  false  CLIENT_ACKNOWLEDGE
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Queue  ${QUEUE}
    Acknowledge
    ${t}=  Get Redelivered
    Should Be True  ${t}
