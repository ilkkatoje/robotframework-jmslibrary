*** Settings ***
Library			JMSLibrary
Documentation   Queue and topic have the same producer. Just to check it will work when both are used in suite.
Suite Setup		Setup
Suite Teardown  Close
Test Setup      Clear Queue  ${QUEUE}
Test Tear Down  Clear Queue  ${QUEUE}

*** Variables ***
${QUEUE}    	      QUEUE.JMSLIBRARY.TEST
${TOPIC}    	      TOPIC.JMSLIBRARY.TEST
${CLIENT_ID}          acceptancetest
${SUBSCRIPTION_NAME}  acceptance-subscription
${BODY_TEXT}          Hello world!
${PROPERTY_NAME}   cProperty
${PROPERTY_VALUE}  1234

*** Keywords ***

Setup
    Connect  org.apache.activemq.jndi.ActiveMQInitialContextFactory  tcp://localhost:61616?jms.useAsyncSend=false
    Set Client Id  ${CLIENT_ID}
    Initialize Session
    Start

*** Test Cases ***

Send and Receive TextMessage
    Create Text Message  ${BODY_TEXT}
    Send To Queue  ${QUEUE}
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Queue  ${QUEUE}
    ${body}=  Get Text
    Should Be Equal  ${BODY_TEXT}  ${body}

Durable Topic Subscriber
    Subscribe Durable  ${TOPIC}   ${SUBSCRIPTION_NAME}
    Create Text Message  ${BODY_TEXT}
    Set String Property  ${PROPERTY_NAME}  ${PROPERTY_VALUE}  
    Send To Topic  ${TOPIC}
    Wait Until Keyword Succeeds  10 s  1 s  Receive From Topic
    ${pid}=  Get String Property  ${PROPERTY_NAME}
    Should be Equal  ${pid}  ${PROPERTY_VALUE}
    Unsubscribe Durable  ${SUBSCRIPTION_NAME}
