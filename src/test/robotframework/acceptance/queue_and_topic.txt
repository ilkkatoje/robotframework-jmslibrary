*** Settings ***
Library         String
Library			JMSLibrary  ${INITIAL_CONTEXT_FACTORY}  ${PROVIDER_URL}
Documentation   Queue and topic have the same producer. Just to check it will work when both are used in suite.
Suite Setup		Setup
Suite Teardown  Close Connection
Test Setup      Clear Queue  ${QUEUE}
Test Tear Down  Clear Queue  ${QUEUE}

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${PROVIDER_URL}             tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC_DURABLE}            TOPIC.JMSLIBRARY.DURABLE.TEST
${CLIENT_ID}                acceptancetest
${SUBSCRIPTION_NAME}        acceptance-subscription
${BODY_TEXT}                Hello world!
${PROPERTY_NAME}            cProperty
${PROPERTY_VALUE}           1234

*** Keywords ***
Setup
    Connect
    Set Client Id  ${CLIENT_ID}
    Initialize Session
    Start

*** Test Cases ***
Send and Receive TextMessage
    Create Text Message  ${BODY_TEXT}
    Send To Queue  ${QUEUE}
    Receive From Queue  ${QUEUE}
    ${body}=  Get Text
    Should Be Equal  ${BODY_TEXT}  ${body}

Durable Topic Subscriber
    ${pvalue}=  Generate Random String
    Subscribe Durable  ${TOPIC_DURABLE}   ${SUBSCRIPTION_NAME}
    Create Text Message  ${BODY_TEXT}
    Set String Property  ${PROPERTY_NAME}  ${pvalue}
    Send To Topic  ${TOPIC_DURABLE}
    Receive From Topic
    ${pid}=  Get String Property  ${PROPERTY_NAME}
    Should be Equal  ${pid}  ${pvalue}
    Unsubscribe Durable  ${SUBSCRIPTION_NAME}
