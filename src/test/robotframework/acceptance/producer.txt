*** Settings ***
Documentation   Test suite which deals with settings related to session scope, hence modifications at some test case can have side effects at another one. Be careful.
Library			JMSLibrary
Suite Setup     Init Provider  ${INITIAL_CONTEXT_FACTORY}  ${JNDI_PROVIDER_URL}  connect=true  start=true
Suite Teardown	Close Connection
Test Setup      Clear Queue Once  ${QUEUE}
Test Tear Down  Clear Queue Once  ${QUEUE}

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${JNDI_PROVIDER_URL}        tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TEXT}                     Hello world!

*** Test Cases ***
Persistent Delivery Mode with Number
    Set Producer Delivery Mode  2
    ${v}=  Get Producer Delivery Mode
    Should Be Equal  PERSISTENT  ${v}
	Create Text Message  ${TEXT}
	Send To Queue  ${QUEUE}
	Receive Once From Queue  ${QUEUE}
	${v}=  Get JMS Delivery Mode
    Should Be Equal  PERSISTENT  ${v}

Non-Persistent Delivery Mode with Number
    Set Producer Delivery Mode  1
    ${v}=  Get Producer Delivery Mode
    Should Be Equal  NON_PERSISTENT  ${v}
	Create Text Message  ${TEXT}
	Send To Queue  ${QUEUE}
	Receive Once From Queue  ${QUEUE}
	${v}=  Get JMS Delivery Mode
    Should Be Equal  NON_PERSISTENT  ${v}

Persistent Delivery Mode
    Set Producer Delivery Mode  PERSISTENT
    ${v}=  Get Producer Delivery Mode
    Should Be Equal  PERSISTENT  ${v}
	Create Text Message  ${TEXT}
	Send To Queue  ${QUEUE}
	Receive Once From Queue  ${QUEUE}
	${v}=  Get JMS Delivery Mode
    Should Be Equal  PERSISTENT  ${v}

Non-Persistent Delivery Mode
    Set Producer Delivery Mode  NON_PERSISTENT
    ${v}=  Get Producer Delivery Mode
    Should Be Equal  NON_PERSISTENT  ${v}
	Create Text Message  ${TEXT}
	Send To Queue  ${QUEUE}
	Receive Once From Queue  ${QUEUE}
	${v}=  Get JMS Delivery Mode
    Should Be Equal  NON_PERSISTENT  ${v}

Producer TimeToLive and Message Expiration
    Create Text Message  ${TEXT}
    Set Producer Time To Live  20000
    Send To Queue  ${QUEUE}
    Receive Once From Queue  ${QUEUE}
    ${v}=  Get JMS Expiration
    ${e}=  Evaluate  ${v} > 0
    Should Be True  ${e}

Priority
    [Setup]  Init Producer
    Set Producer Priority  7
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Receive Once From Queue  ${QUEUE}
    ${p}=  Get JMS Priority
    Should Be Equal As Strings  ${p}  7

Producer Initialization
    Init Producer  NON_PERSISTENT  0  1234
    ${dm}=  Get Producer Delivery Mode
    ${p}=  Get Producer Priority
    ${ttl}=  Get Producer Time To Live
    Should be Equal  ${dm}  NON_PERSISTENT
    Should be Equal As Integers  ${p}  0
    Should be Equal As Integers  ${ttl}  1234
