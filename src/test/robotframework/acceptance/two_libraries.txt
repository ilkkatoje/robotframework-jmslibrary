*** Settings ***
Library         String
Library         JMSLibrary  WITH NAME  jms1
Library         JMSLibrary  WITH NAME  jms2
Suite Setup     Initialize Suite
Suite Teardown  Deinitialize Suite

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${JNDI_PROVIDER_URL}        tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC}                    TOPIC.JMSLIBRARY.TEST

*** Test Cases ***
Queue Send and Receive TextMessage
    [Setup]  jms1.Clear Queue Once  ${QUEUE}
    ${text}=  Generate Random String
    jms1.Create Text Message  ${text}
    jms1.Send To Queue  ${QUEUE}
    jms2.Receive Once From Queue  ${QUEUE}
    ${body}=  jms2.Get Text
    Should Be Equal  ${body}  ${text}
    [Teardown]  jms1.Clear Queue Once  ${QUEUE}

Topic Send and Receive TextMessage
    [Setup]  jms2.Init Topic Consumer  ${TOPIC}
    ${text}=  Generate Random String
    jms1.Create Text Message  ${text}
    jms1.Send To Topic  ${TOPIC}
    jms2.Receive
    ${body}=  jms2.Get Text
    Should Be Equal  ${body}  ${text}
    [Teardown]  jms2.Close Consumer

Clear With Transactions
	jms1.Init Session  true  SESSION_TRANSACTED
    jms2.Init Session  true  SESSION_TRANSACTED
    jms2.Init Queue Consumer  ${QUEUE}
    ${text1}=  Generate Random String
    jms1.Create Text Message  ${text1}
    jms1.Send To Queue  ${QUEUE}
    jms1.Send To Queue  ${QUEUE}
    jms1.Commit
    ${c}=  jms2.Clear
    Should be Equal As Integers  ${c}  2
    jms1.Send To Queue  ${QUEUE}
    jms1.Commit
    ${c}=  jms2.Clear
    jms2.Commit
    Should be Equal As Integers  ${c}  1
    ${c}=  jms2.Clear
    jms2.Commit
    Should be Equal As Integers  ${c}  0


*** Keywords ***
Initialize Suite
    jms1.Init Provider  ${INITIAL_CONTEXT_FACTORY}  ${JNDI_PROVIDER_URL}  connect=true  start=true
    jms2.Init Provider  ${INITIAL_CONTEXT_FACTORY}  ${JNDI_PROVIDER_URL}  connect=true  start=true

Deinitialize Suite
    jms1.Close Connection
    jms2.Close Connection
