*** Settings ***
Library         JMSLibrary  ${INITIAL_CONTEXT_FACTORY}  ${PROVIDER_URL}  WITH NAME  jms1
Library         JMSLibrary  ${INITIAL_CONTEXT_FACTORY}  ${PROVIDER_URL}  WITH NAME  jms2
Suite Setup     Initialize Suite
Suite Teardown  Deinitialize Suite
Test Setup      jms1.Clear Queue  ${QUEUE}
Test Tear Down  jms1.Clear Queue  ${QUEUE}

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${PROVIDER_URL}             tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC}                    TOPIC.JMSLIBRARY.TEST
${BODY_TEXT_1}              Hello world 1!
${BODY_TEXT_2}              Hello world 2!

*** Test Cases ***
Queue Send and Receive TextMessage
    jms1.Create Text Message  ${BODY_TEXT_1}
    jms1.Send To Queue  ${QUEUE}
    jms2.Receive From Queue  ${QUEUE}
    ${body}=  jms2.Get Text
    Should Be Equal  ${BODY_TEXT_1}  ${body}

Topic Send and Receive TextMessage
    jms2.Subscribe  ${TOPIC}
    jms1.Create Text Message  ${BODY_TEXT_2}
    jms1.Send To Topic  ${TOPIC}
    jms2.Receive From Topic
    ${body}=  jms2.Get Text
    Should Be Equal  ${BODY_TEXT_2}  ${body}
    jms2.Unsubscribe

*** Keywords ***
Initialize Suite
    jms1.Connect And Start
    jms2.Connect And Start

Deinitialize Suite
    jms1.Close
    jms2.Close
