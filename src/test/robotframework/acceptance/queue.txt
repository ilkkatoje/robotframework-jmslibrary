*** Settings ***
Library			OperatingSystem
Library         String
Library			JMSLibrary
Suite Setup     Init Provider  ${INITIAL_CONTEXT_FACTORY}  ${JNDI_PROVIDER_URL}  connect=true  start=true
Suite Teardown  Close Connection
Test Setup      Clear Queue Once  ${QUEUE}
Test Tear Down  Clear Queue Once  ${QUEUE}

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${JNDI_PROVIDER_URL}        tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${REPLY2QUEUE}              QUEUE.JMSLIBRARY.TEST.REPLY
${TOPIC}                    TOPIC.JMSLIBRARY.TEST
${TEXT}                Hello world!

*** Test Cases ***
Send and Clear
    ${c}=  Clear Queue Once  ${QUEUE}
    Should Be Equal As Integers  0  ${c}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    ${c}=  Clear Queue Once  ${QUEUE}
    Should Be Equal As Integers  3  ${c}

Queue Depth
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    ${c}=  Queue Depth  ${QUEUE}
    Should Be Equal As Integers  2  ${c}

Double Send
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    ${v1}=  Get Jms Message Id
    Send To Queue  ${QUEUE}
    ${v2}=  Get Jms Message Id
    Should Not Be Equal  ${v1}  ${v2}

Send and Receive Once TextMessage
    ${text}=  Generate Random String
    Create Text Message  ${text}
    Send To Queue  ${QUEUE}
    Receive Once From Queue  ${QUEUE}
    ${body}=  Get Text
    Should Be Equal  ${body}  ${text}

Send and Receive Once TextMessage With Timeout
    ${text}=  Generate Random String
    Create Text Message  ${text}
    Send To Queue  ${QUEUE}
    Receive Once From Queue  ${QUEUE}  1000
    ${body}=  Get Text
    Should Be Equal  ${body}  ${text}
