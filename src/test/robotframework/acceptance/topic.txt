*** Settings ***
Library         String
Library			JMSLibrary
Suite Setup		Setup
Suite Teardown  Close Connection

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${JNDI_PROVIDER_URL}        tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC}                    TOPIC.JMSLIBRARY.TEST
${TOPIC_D}                  TOPIC.JMSLIBRARY.DURABLE.TEST
${CLIENT_ID}                acceptancetest
${SUBSCRIPTION_NAME}        acceptance-subscription
${TEXT}                Hello world!

*** Keywords ***
Setup
    Init Provider  ${INITIAL_CONTEXT_FACTORY}  ${JNDI_PROVIDER_URL}
    Connect
    Set Client Id  ${CLIENT_ID}
    Init Session
    Start

*** Test Cases ***
Topic Subscriber
    [Setup]  Init Topic Consumer  ${TOPIC}
    ${text}=  Generate Random String
    Create Text Message  ${text}
    Send To Topic  ${TOPIC}
    Receive
    ${body}=  Get Text
    Should be Equal  ${body}  ${text}
    [Teardown]  Close Consumer

Topic Subscriber With Receive Timeout
    [Setup]  Init Topic Consumer  ${TOPIC}
    ${text}=  Generate Random String
    Create Text Message  ${text}
    Send To Topic  ${TOPIC}
    Receive  1000
    ${body}=  Get Text
    Should be Equal  ${body}  ${text}
    [Teardown]  Close Consumer

Topic Subscriber Clear
    [Setup]  Init Topic Consumer  ${TOPIC}
    Create Text Message  ${TEXT}  
    Send To Topic  ${TOPIC}
    Send To Topic  ${TOPIC}
    Send To Topic  ${TOPIC}
    ${c}=  Clear
    Should be Equal As Integers  ${c}  3
    [Teardown]  Close Consumer

Durable Topic Subscriber
    Init Durable Subscriber  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    ${text}=  Generate Random String
    Create Text Message  ${text}
    Send To Topic  ${TOPIC_D}
    Receive
    ${body}=  Get Text
    Should be Equal  ${body}  ${text}
    Close Consumer
    Unsubscribe  ${SUBSCRIPTION_NAME}

Durable Topic Subscriber with Unsubscribe
    Init Durable Subscriber  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    Close Consumer
    ${text}=  Generate Random String
    Create Text Message  ${text}
    Send To Topic  ${TOPIC_D}
    Init Durable Subscriber  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    Receive
    ${body}=  Get Text
    Should be Equal  ${body}  ${text}
    Close Consumer
    Unsubscribe  ${SUBSCRIPTION_NAME}

Durable Topic Subscriber Clear
    Init Durable Subscriber  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    Create Text Message  ${TEXT}
    Send To Topic  ${TOPIC_D}
    Send To Topic  ${TOPIC_D}
    Send To Topic  ${TOPIC_D}
    Send To Topic  ${TOPIC_D}
    ${c}=  Clear
    Should be Equal As Integers  ${c}  4
    Close Consumer
    Unsubscribe  ${SUBSCRIPTION_NAME}
