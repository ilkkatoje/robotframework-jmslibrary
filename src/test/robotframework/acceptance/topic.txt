*** Settings ***
Library         String
Library			JMSLibrary  ${INITIAL_CONTEXT_FACTORY}  ${PROVIDER_URL}
Suite Setup		Setup
Suite Teardown  Close

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${PROVIDER_URL}             tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC}                    TOPIC.JMSLIBRARY.TEST
${TOPIC_D}                  TOPIC.JMSLIBRARY.DURABLE.TEST
${CLIENT_ID}                acceptancetest
${SUBSCRIPTION_NAME}        acceptance-subscription
${BODY_TEXT}                Hello world!
${PROPERTY_NAME}            cProperty
${PROPERTY_VALUE}           1234

*** Keywords ***
Setup
    Connect  org.apache.activemq.jndi.ActiveMQInitialContextFactory  tcp://localhost:61616?jms.useAsyncSend=false
    Set Client Id  ${CLIENT_ID}
    Initialize Session
    Start

*** Test Cases ***
Topic Subscriber
    ${pvalue}=  Generate Random String
    Subscribe  ${TOPIC}
    Create Text Message  ${BODY_TEXT}
    Set String Property  ${PROPERTY_NAME}  ${pvalue}
    Send To Topic  ${TOPIC}
    Receive From Topic
    ${pid}=  Get String Property  ${PROPERTY_NAME}
    Should be Equal  ${pid}  ${pvalue}
    Unsubscribe

Topic Subscriber With Receive Timeout
    ${pvalue}=  Generate Random String
    Subscribe  ${TOPIC}
    Create Text Message  ${BODY_TEXT}
    Set String Property  ${PROPERTY_NAME}  ${pvalue}
    Send To Topic  ${TOPIC}
    Receive From Topic  1000
    ${pid}=  Get String Property  ${PROPERTY_NAME}
    Should be Equal  ${pid}  ${pvalue}
    Unsubscribe

Topic Subscriber Clear 
    Subscribe  ${TOPIC}
    Create Text Message  ${BODY_TEXT}  
    Send To Topic  ${TOPIC}
    Send To Topic  ${TOPIC}
    Send To Topic  ${TOPIC}
    ${c}=  Clear Topic
    Should be Equal As Integers  ${c}  3
    Unsubscribe

Durable Topic Subscriber
    ${pvalue}=  Generate Random String
    Subscribe Durable  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    Create Text Message  ${BODY_TEXT}
    Set String Property  ${PROPERTY_NAME}  ${pvalue}
    Send To Topic  ${TOPIC_D}
    Receive From Topic
    ${pid}=  Get String Property  ${PROPERTY_NAME}
    Should be Equal  ${pid}  ${pvalue}
    Unsubscribe Durable  ${SUBSCRIPTION_NAME}

Durable Topic Subscriber with Unsubscribe
    Subscribe Durable  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    Unsubscribe
    Create Text Message  ${BODY_TEXT}
    Set String Property  ${PROPERTY_NAME}  ${PROPERTY_VALUE}
    Send To Topic  ${TOPIC_D}
    Subscribe Durable  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    Receive From Topic
    ${pid}=  Get String Property  ${PROPERTY_NAME}
    Should be Equal  ${pid}  ${PROPERTY_VALUE}
    Unsubscribe Durable  ${SUBSCRIPTION_NAME}

Durable Topic Subscriber Clear
    Subscribe Durable  ${TOPIC_D}   ${SUBSCRIPTION_NAME}
    Create Text Message  ${BODY_TEXT}
    Set String Property  ${PROPERTY_NAME}  ${PROPERTY_VALUE}
    Send To Topic  ${TOPIC_D}
    Send To Topic  ${TOPIC_D}
    Send To Topic  ${TOPIC_D}
    Send To Topic  ${TOPIC_D}
    ${c}=  Clear Topic
    Should be Equal As Integers  ${c}  4
    Unsubscribe Durable  ${SUBSCRIPTION_NAME}
