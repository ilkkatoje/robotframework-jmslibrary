*** Settings ***
Documentation   Test suite which deals with settings related to session scope, hence modifications at some test case can have side effects at another one. Be careful.
Library         BuiltIn
Library         String
Library			JMSLibrary
Suite Setup     Init Provider  ${INITIAL_CONTEXT_FACTORY}  ${JNDI_PROVIDER_URL}  connect=true  start=true
Suite Teardown	Close Connection
Test Setup      Clear Queue Once  ${QUEUE}
Test Teardown   Clear Queue Once  ${QUEUE}

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${JNDI_PROVIDER_URL}        tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${QUEUE2}                   QUEUE.JMSLIBRARY.TEST.2
${TEXT}                     Hello world!

*** Test Cases ***
Send Client Acknowledged
    Init Session  false  2
    ${text1}=  Generate Random String
    Create Text Message  ${text1}
    Send To Queue  ${QUEUE}
    Init Queue Consumer  ${QUEUE}
    Receive
    Close Consumer
    Init Queue Consumer  ${QUEUE}
    Receive
    Acknowledge
    ${t}=  Get Text
    ${r}=  Get JMS Redelivered
    Should Be Equal As Strings  ${t}  ${text1}
    Should Be True  ${r}
    ${qd}=  Queue Depth  ${QUEUE}
    Should Be Equal As Integers  ${qd}  0

Transaction
    Init Session  true  0
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Rollback
    Send To Queue  ${QUEUE}
    Commit
    Receive Once From Queue  ${QUEUE}
    ${c}=  Clear Queue Once  ${QUEUE}
    Should Be Equal As Integers  ${c}  0

Transaction With Two Receives
    [Setup]  Run Keywords  Init Session  true  SESSION_TRANSACTED  AND  Clear Queue Once  ${QUEUE}
    Init Queue Consumer  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Send To Queue  ${QUEUE}
    Commit
    Receive
    Receive
    Rollback
    Receive  1000
    Receive
    Run Keyword And Expect Error  *  Receive
    Commit
    [Teardown]  Run Keywords  Close Consumer  AND  Clear Queue Once  ${QUEUE}

Commit After Closing Consumers
    [Setup]  Run Keywords  Clear Queue Once  ${QUEUE}  AND  Clear Queue Once  ${QUEUE2}  
    Init Session  true  SESSION_TRANSACTED
    Init Queue Consumer  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Send To Queue  ${QUEUE2}
    Commit
    Init Queue Consumer  ${QUEUE}
    Receive
    Init Queue Consumer  ${QUEUE2}
    Receive
    Close Consumer
    Commit
    ${c1}=  Clear Queue Once  ${QUEUE}
    ${c2}=  Clear Queue Once  ${QUEUE2}
    Should Be Equal As Integers  ${c1}  0
    Should Be Equal As Integers  ${c2}  0
    
Not JMS Redelivered
    [Setup]  Init Session  false  CLIENT_ACKNOWLEDGE
    Init Queue Consumer  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Receive
    ${v}=  Get JMS Redelivered
    Should Not Be True  ${v}
    Acknowledge
    [Teardown]  Close Consumer

JMS Redelivered              
    Init Session  false  CLIENT_ACKNOWLEDGE
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Init Queue Consumer  ${QUEUE}
    Receive
    Init Session  false  CLIENT_ACKNOWLEDGE
    Init Queue Consumer  ${QUEUE}
    Receive
    Acknowledge
    ${t}=  Get JMS Redelivered
    Should Be True  ${t}
