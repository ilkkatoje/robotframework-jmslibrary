*** Settings ***
Library         JMSLibrary
Suite Setup     Init Provider  ${INITIAL_CONTEXT_FACTORY}  ${JNDI_PROVIDER_URL}  connect=true  start=true
Suite Teardown  Close Connection

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${JNDI_PROVIDER_URL}        tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC}                    TOPIC.JMSLIBRARY.TEST
${TEXT}                     Hello world!

*** Test Cases ***
Send And Receive Once TextMessage With Queue
    [Setup]  Clear Queue Once  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Receive Once From Queue  ${QUEUE}
    ${body}=  Get Text
    Should Be Equal  ${body}  ${TEXT}

Send And Receive BytesMessage With Queue
    [Setup]  Run Keywords  Init Queue Consumer  ${QUEUE}  AND  Clear
    Create Bytes Message  ${TEXT}  UTF-8
    Send To Queue  ${QUEUE}
    Receive
    ${body}=  Get Bytes As String  UTF-8
    Should Be Equal  ${body}  ${TEXT}
    [Teardown]  Close Consumer

Send and Receive BytesMessage With Topic
    [Setup]  Init Topic Consumer  ${TOPIC}
    Create Bytes Message  ${TEXT}  UTF-8
    Send To Topic  ${TOPIC}
    Receive
    ${body}=  Get Bytes As String  UTF-8
    Should Be Equal  ${body}  ${TEXT}
    [Teardown]  Close Consumer
