*** Settings ***
Library         JMSLibrary  ${INITIAL_CONTEXT_FACTORY}  ${PROVIDER_URL}
Suite Setup     Connect And Start
Suite Teardown  Close Connection

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${PROVIDER_URL}             tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC}                    TOPIC.JMSLIBRARY.TEST
${TEXT}                     Hello world!

*** Test Cases ***
Queue Send and Receive TextMessage
    [Setup]  Clear Queue Once  ${QUEUE}
    Create Text Message  ${TEXT}
    Send To Queue  ${QUEUE}
    Receive Once From Queue  ${QUEUE}  
    ${body}=  Get Text
    Should Be Equal  ${body}  ${TEXT}

Topic Send and Receive TextMessage
    [Setup]  Init Topic Consumer  ${TOPIC}
    Create Text Message  ${TEXT}
    Send To Topic  ${TOPIC}
    Receive
    ${body}=  Get Text
    Should Be Equal  ${body}  ${TEXT}
    [Teardown]  Close Consumer
