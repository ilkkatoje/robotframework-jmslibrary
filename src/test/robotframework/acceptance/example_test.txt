*** Settings ***
Library         JMSLibrary  ${INITIAL_CONTEXT_FACTORY}  ${PROVIDER_URL}
Suite Setup     Connect And Start
Suite Teardown  Close
Test Setup      Clear Queue  ${QUEUE}
Test Tear Down  Clear Queue  ${QUEUE}

*** Variables ***
${INITIAL_CONTEXT_FACTORY}  org.apache.activemq.jndi.ActiveMQInitialContextFactory
${PROVIDER_URL}             tcp://localhost:61616?jms.useAsyncSend=false
${QUEUE}                    QUEUE.JMSLIBRARY.TEST
${TOPIC}                    TOPIC.JMSLIBRARY.TEST
${BODY_TEXT}                Hello world!

*** Test Cases ***
Queue Send and Receive TextMessage
    Create Text Message  ${BODY_TEXT}
    Send To Queue  ${QUEUE}
    Receive From Queue  ${QUEUE}
    ${body}=  Get Text
    Should Be Equal  ${BODY_TEXT}  ${body}

Topic Send and Receive TextMessage
    Subscribe  ${TOPIC}
    Create Text Message  ${BODY_TEXT}
    Send To Topic  ${TOPIC}
    Receive From Topic
    ${body}=  Get Text
    Should Be Equal  ${BODY_TEXT}  ${body}
    Unsubscribe
